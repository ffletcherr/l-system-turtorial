from random import uniform as uni
from random import randint as rint

age = 20
blen1, blen2 = 2, 5
bdeg1, bdeg2 = 35, 55
baseWidth = 0.8
crowndensity = 4
crv1, crv2 = 5, 55
bprob = 0.8
cprob = 0.5
div = 4
trunklen = 3

Axiom: SetWidth(baseWidth)@GcF(3,baseWidth/div + 0.15)F(trunklen,baseWidth/div)trunk(age)@Ge

derivation length: 50
production:

trunk(t):
  nproduce F(uni(0.1, 2))
  if uni(0,1) < bprob:
    nproduce [randrot(-180,180, bdeg1, bdeg2)branch(rint(t,t+4))]
  if t > 0 :
    produce trunk(t-1)
  else:
    produce crown(3)


branch(t):
  nproduce SetWidth(baseWidth/(div*2))curve(crv1, crv2)F(uni(blen1, blen2))
  if uni(0,1) < cprob:
    nproduce crown(crowndensity)
  if t > 0 :
    produce branch(t-1)
  else:
    produce crown(crowndensity)
    
crown(w):
  if w > 0:
    for _ in range(w):
      nproduce [;randrot(-180,180,-100,2)F(uni(0.5,2),baseWidth/10)[F(uni(1,3),baseWidth/40)F(uni(1,3))]]crown(w-1)
 

curve(r,p):
  nproduce -(rint(-r,r))&(rint(-p,p))

randrot(r1,r2,t1,t2):
	produce  /(uni(r1,r2))+(uni(t1,t2))

interpretation:


endlsystem
